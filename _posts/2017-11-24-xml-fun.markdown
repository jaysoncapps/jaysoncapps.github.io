---
layout: post
title:  "Fun with XML, XSLT and OWL"
date:   2017-11-24 15:43:44 -0600
categories: XML
author: "Jayson Capps"
---

<img class="img-thumbnail" src="/assets/img/blog/student-new-screenshot.png" />

<p>My goal was to use the News Story Index/XML generated by Cascade and modify one of the News Formats developed by UCAM to show only new stories that contained the keyword, "Students". Below is the page region where I placed the "News Story" Index Block and newly created "News Format - Student News - OWL" format.</p>

<img class="img-thumbnail" src="/assets/img/blog/student-news-output-screenshot.png" />

Here is the code for the XSLT. I modified the "_common/v2/formats/news/News Main Page". 

{% highlight xml %}
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" exclude-result-prefixes="xalan" version="1.0" xmlns:xalan="http://xml.apache.org/xalan" xmlns:xd="http://www.oxygenxml.com/ns/doc/xsl">
  <xsl:include href="site://_common/formats/include/format-date"/>
  
  <xsl:param name="itemcount">30</xsl:param>
  <xsl:template match="/system-index-block">
    <div class="news">
      <h2>Recent Student News</h2>
      
      <!-- Start of OWL Carousel -->
      <div class="owl-carousel">
        <xsl:apply-templates select="system-page[not(contains(path, '_internal'))][name != 'index']">
          <xsl:sort order="descending" select="start-date"/>
        </xsl:apply-templates>
      </div>
      <!-- End of OWL Carousel -->
      <p> <a class="btn btn-default" href="/news/index">More News</a> </p>
    </div>
  </xsl:template>
  
  <xsl:template match="system-page">
    
    <!-- list of all images in story -->
    <xsl:variable name="images">
    <images>
      <xsl:apply-templates mode="array" select=".//img"/>
    </images>
    </xsl:variable>
    
    <!-- displays all news stories by meta keyword Students -->
    <xsl:if test="position() &lt;= $itemcount and contains(.,'Students')">
        <div class="media" style="margin-bottom:15px;">
          <xsl:apply-templates mode="thumbnail" select="xalan:nodeset($images)/images/img[1]"/>
          <div class="media-body">
            <p><strong><a href="{path}"><xsl:value-of select="display-name"/></a></strong><br/>
              <em>
              <xsl:call-template name="format-date">
                <xsl:with-param name="date" select="start-date"/>
                <xsl:with-param name="mask">longDate</xsl:with-param>
              </xsl:call-template>
              </em><br/>
            </p>
            
          </div>
        </div>
    </xsl:if>
  </xsl:template>
  
  <xsl:template match="img" mode="array">
    <xsl:copy-of select="."/>
  </xsl:template>
  <xsl:template match="img" mode="thumbnail">
    <img alt="{@alt}" class="media-object" src="{@src}"/>
  </xsl:template>
  <xsl:template name="RemoveHtmlTags">
    <xsl:param name="html"/>
    <xsl:choose>
      <xsl:when test="contains($html, '&lt;')">
        <xsl:value-of select="substring-before($html, '&lt;')"/> 
        <!-- Recurse through HTML -->
        <xsl:call-template name="RemoveHtmlTags">
          <xsl:with-param name="html" select="substring-after($html, '&gt;')"/>
          
        </xsl:call-template>
      </xsl:when>
      <xsl:otherwise>
        <xsl:value-of select="$html"/>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>
</xsl:stylesheet>
{% endhighlight %}

I then used <a href="https://owlcarousel2.github.io/OwlCarousel2/" target="_blank">Owl Carousel</a> to wrap and display the news stories. I used a carousel for its flexibility. I needed a container that would be able to display 5-30 possible news items while still being able to display them in a single row within a grid layout.

To see the live project, please go to the <a href="http://lawschool.unm.edu/students/" target="_blank">UNM School of Law Campus Life</a> page.


